sub init()
    m.top.observeField("view", "renderView")
    ' TODO - just re-render statuses on data change
    m.top.observeField("privacyManagerViewData", "renderRightCol")
end sub

sub observeCategoryList(event as Object)
    'CategoryDetailsView
    item = m.categoryList.content.getChild(event.getData())

    changeView("CategoryDetailsView", m.top.privacyManagerViewData.categories[item.id])
end sub

sub renderRightCol()
    if m.categoryList <> invalid then
        ' this re-renders if consent changes, remove old component to re-render
        m.colRight.removeChild(m.categoryList)
        m.categoryList = invalid
    end if 

    m.categoryList = createObject("roSGNode", "SpButtonList")
    m.categoryList.width = m.colRightWidth - scalePixelDimension(50)

    buttons = []
    for each categoryId in m.top.privacyManagerViewData.categories
        buttonSettings = { 
            on: m.top.privacyManagerViewData.categories[categoryId]["__enabled"]
            settings: {} 
        } 
        buttonSettings.settings.append(m.components.button_category.settings)
        buttonSettings.id = categoryId
        buttonSettings.settings.text = m.top.privacyManagerViewData.categories[categoryId].name

        buttons.push(buttonSettings)
    end for

    m.categoryList.buttonComponents = buttons

    m.colRight.appendChild(m.categoryList)
    m.rightColFocus = m.categoryList
    m.categoryList.observeField("itemSelected", "observeCategoryList")
end sub

sub renderView(event as Object)
    getPrivacyManagerViewData(2)
    m.colLeft = m.top.findNode("col-left")
    m.colRight = m.top.findNode("col-right")

    view = event.getData()
    mapComponents(view)

    renderLogo()
    renderNav(["save_and_exit"])

    ' TODO add loader to the right col until it is rendered

    m.nav.setFocus(true)
end sub