sub init()
    m.top.observeField("userConsent", "ingestConsent")
    m.top.observeField("acceptCategory", "acceptCategory")
    m.top.observeField("acceptVendor", "acceptVendor")
    m.top.observeField("rejectCategory", "rejectCategory")
    m.top.observeField("rejectVendor", "rejectVendor")

    m.rejectedCategories = []
    m.rejectedVendors = []
end sub

sub acceptCategory(event as Object)
    acceptedCategory = event.getData()
    for i = 0 to (m.rejectedCategories.count() - 1) step 1
        if m.rejectedCategories[i] = acceptedCategory then
            m.rejectedCategories.delete(i)
            exit for
        end if
    end for
end sub

sub acceptVendor(event as Object)
    acceptedVendor = event.getData()
    for i = 0 to (m.rejectedVendors.count() - 1) step 1
        if m.rejectedVendors[i] = acceptedVendor then
            m.rejectedVendors.delete(i)
            exit for
        end if
    end for
end sub

sub ingestConsent(event as Object)
    userConsent = event.getData()

    if userConsent.rejectedCategories <> invalid then
        m.rejectedCategories = userConsent.rejectedCategories
    end if
    if userConsent.rejectedVendors <> invalid then
        m.rejectedVendors = userConsent.rejectedVendors
    end if
end sub

sub rejectCategory(event as Object)
    found = false
    rejectedCategory = event.getData()
    for each cat in m.rejectedCategories
        if rejectedCategory = cat then
            found = true
        end if
    end for

    if found = false then
        m.rejectedCategories.push(rejectedCategory)
    end if
end sub

sub rejectVendor(event as Object)
    found = false
    rejectedVendor = event.getData()
    for each vendor in m.rejectedVendors
        if rejectedVendor = vendor then
            found = true
        end if
    end for

    if found = false then
        m.rejectedVendors.push(rejectedVendor)
    end if
end sub

function getSaveAndExitVariables() as Object
    print m.rejectedVendors
    print m.rejectedCategories

    return {
        "lan": m.global.config.consentLanguage,
        "privacyManagerId": m.top.messageId.toStr(),
        "rejectedCategories": m.rejectedCategories,
        "rejectedVendors": m.rejectedVendors
    }
end function