sub init()
    m.top.observeField("view", "renderView")
    ' TODO - just re-render statuses on data change
    m.top.observeField("privacyManagerViewData", "renderRightCol")
end sub

sub observeVendorList(event as Object)
    index = event.getData()

    if index <> invalid then
        item = m.vendorList.buttonComponents[index]
        changeView("VendorDetailsView", m.top.privacyManagerViewData.vendors[item.id])
    end if
end sub

sub renderRightCol()
    if m.vendorList <> invalid then
        ' this re-renders if consent changes, remove old component to re-render
        m.colRight.removeChild(m.vendorList)
        m.vendorList = invalid
    end if 

    m.vendorList = createObject("roSGNode", "SpButtonList")
    m.vendorList.width = m.colRightWidth - scalePixelDimension(50)

    buttons = []
    for each vendorId in m.top.privacyManagerViewData.vendors
        buttonSettings = { 
            on: m.top.privacyManagerViewData.vendors[vendorId]["__enabled"]
            settings: {} 
        } 
        buttonSettings.settings.append(m.components.button_vendor.settings)
        buttonSettings.id = vendorId
        buttonSettings.settings.text = m.top.privacyManagerViewData.vendors[vendorId].name

        buttons.push(buttonSettings)
    end for

    m.vendorList.buttonComponents = buttons

    m.colRight.appendChild(m.vendorList)
    m.rightColFocus = m.vendorList
    m.vendorList.observeField("itemSelected", "observeVendorList")
end sub

sub renderView(event as Object)
    getPrivacyManagerViewData(2)

    m.colLeft = m.top.findNode("col-left")
    m.colRight = m.top.findNode("col-right")

    view = event.getData()
    mapComponents(view)

    renderLogo()
    renderNav(["accept_all", "save_and_exit"])

    ' TODO add loader to the right col until it is rendered

    m.nav.setFocus(true)
end sub