import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.functionName = "privacyManagerView"
end sub

sub privacyManagerView()
    ' don't re-retrieve data
    if m.top.data <> invalid then
        return
    end if 

    if m.top.messageCategory = 1 then
        url = m.global.config.baseEndpoint + "/consent/tcfv2/privacy-manager/privacy-manager-view"
    else if m.top.messageCategory = 2 then
        url = m.global.config.baseEndpoint + "/ccpa/privacy-manager/privacy-manager-view"
    else
        return
    end if 

    url = addQueryParams(
        url,
        {
            "consentLanguage": m.top.consentLanguage,
            "siteId": m.top.propertyId
        }
    )

    response = makeRequest(url, "GET")

    if response <> invalid then
        data = {
            categories: {},
            vendors: {}
        }

        for each category in response.categories
            data.categories[category._id] = category
        end for

        for each vendor in response.vendors
            data.vendors[vendor._id] = vendor
        end for

        m.top.data = data
    end if
end sub