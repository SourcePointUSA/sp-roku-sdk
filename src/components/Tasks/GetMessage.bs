sub init()
    m.top.functionName = "getMessage"
end sub

sub getMessage()
    requestBody = {
        "accountId": m.top.accountId,
        "authId": m.top.authId,
        "campaignEnv": m.top.campaignEnv,
        "campaigns": {},
        "clientMMSOrigin": m.top.clientMMSOrigin,
        "consentLanguage": m.top.consentLanguage,
        "hasCSP": true, 
        "includeData": {
            "localState": { "type": "string" },
            "customVendorsResponse": { "type": "string" }
        },
        "localState": m.top.localState,
        "propertyHref": m.top.propertyHref,
        "pubData": m.top.pubData,
        "requestUUID": m.top.requestUUID
    }

    for each l in m.top.legislationsEnabled
        requestBody.campaigns[l] = {}
    end for

    response = makeRequest(m.top.url, "POST", requestBody)

    if response <> invalid then
        messages = []
        for each c in response.campaigns
            if c.message <> invalid then
                messages.push({ message: c.message, messageMetaData: c.messageMetaData })
            end if
        end for

        m.top.messages = messages
    end if 
end sub