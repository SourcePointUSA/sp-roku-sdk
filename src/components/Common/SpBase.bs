import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    usableWidth = scalePixelDimension(1180)
    m.colLeftWidth = usableWidth / 2
    m.colRightWidth = usableWidth / 2
    
    translationMap = {
        "col-left-width-holder": [0,0,m.colLeftWidth,0],
        "col-right-width-holder": [0,0,m.colRightWidth,0],
        "screen-title": [usableWidth/2, 0],
        ' "screen-title": function(node, sWidth, sHeight) as Object
        '     return [1180/2, 0] 
        ' end function,
        "col-layout": [0, 50]
    }
    scalePixelDimensions(m.top, translationMap)

    m.componentIdMap = {
        "AcceptAllButton" : "accept_all",
        "Body": "privacy_policy_body",
        "BackButton": "button_nav_back",
        "CategoriesDescriptionText": "text_category_description",
        "CategoriesSubDescriptionText": "text_category_sub_description",
        "HeaderText": "text_header",
        "LogoImage": "image_logo",
        "OnButton": "button_on",
        "OffButton": "button_off",
        "NavCategoriesButton": "button_nav_categories",
        "NavPrivacyPolicyButton": "button_nav_privacy_policy",
        "NavVendorsButton": "button_nav_vendors",
        "PublisherDescription": "text_publisher_description",
        "SaveButton": "save_and_exit",
    }
    m.components = {}
    ' m.navViewMap : maps nav button IDs to views they should link to
    ' m.componentIdMap : fills in m.components mapping JSON id to our IDs
    ' m.rightColFocus : what should focus in the right column when the right arrow is pressed

    m.top.translation=[scalePixelDimension(50),scalePixelDimension(50)]
end sub

sub changeView(viewName as String, viewData = invalid as Object)
    spMessage = m.top.getParent()
    spMessage.changeViewDataExtra = viewData
    spMessage.changeView = viewName
end sub

sub getPrivacyManagerViewData(callbackFn, messageCategoryId)
    m.pmvCallback = callbackFn
    if m.privacyManagerViewData = invalid then
        m.privacyManagerViewTask = m.top.privacyManagerViewTask
        m.privacyManagerViewTask.messageCategory = messageCategoryId
        m.privacyManagerViewTask.propertyId = m.global.config.propertyId
        m.privacyManagerViewTask.control = "RUN"
        m.privacyManagerViewTask.observeField("state", "setCategoryData")
    else
        m.pmvCallback()
    end if
end sub

sub mapComponents(view as object)
    m.components.view = view
    if m.componentIdMap <> invalid then
        for each component in view.children
            if m.componentIdMap[component.id] <> invalid then
                m.components[m.componentIdMap[component.id]] = component
            end if
        end for
    end if
end sub

' TODO - add logic to handle "choice action" buttons
sub observeNav(event as Object)
    selectedButton = m.nav.getChild(event.getData())

    if m.navViewMap <> invalid and m.navViewMap[selectedButton.id] <> invalid then
        changeView(m.navViewMap[selectedButton.id])
    else
        consentTask = createObject("roSGNode", "ConsentTask")
        consentTask.messageCategory = m.top.messageMetadata.categoryId

        if selectedButton.id = "accept_all" then
            consentTask.action = "accept"
        else if selectedButton.id = "reject_all" then
            consentTask.action = "reject"
        else if selectedButton.id = "save_and_exit" then
            consentTask.action = "save_and_exit"
            consentTask.saveAndExitVariables = m.top.userConsentNode.callFunc("getSaveAndExitVariables")
        end if 

        consentTask.control = "RUN"
        consentTask.observeField("userConsent", "setConsent")
    end if
end sub

function onKeyEvent(key as String, press as Boolean) as Boolean
    if key = "left" and press = true and m.nav <> invalid then
        m.nav.setFocus(true)
        return true
    else if key = "right" and press = true and m.rightColFocus <> invalid then
        m.rightColFocus.setFocus(true)
    end if 

    return false
end function

sub renderBase()
    if m.components.view.settings.style.backgroundColor <> invalid then
        spScene = m.top.getScene()
        spScene.backgroundUri = ""
        spScene.backgroundColor = colorConvert(m.components.view.settings.style.backgroundColor)
    end if 
end sub

sub renderLogo()
    renderBase()

    if m.components.text_header <> invalid then
        m.top.findNode("screen-title").settings = m.components.text_header.settings
        m.top.findNode("screen-title").textComponent.horizOrigin = "center"
        ' m.top.findNode("screen-title").text = m.components.text_header.settings.text
    end if

    if m.components.image_logo <> invalid then
        component = createObject("roSGNode", "Poster")
        component.id = "image_logo"

        if m.components.image_logo.settings.style.width <> invalid then
            component.loadDisplayMode = "limitSize"
            component.loadWidth = m.components.image_logo.settings.style.width
            component.loadHeight = 540
        end if 

        component.uri = m.components.image_logo.settings.url
        ' TODO - set this to something for loading
        ' component.loadingBitmapUri = 
        m.colLeft.appendChild(component)
    end if
end sub

sub renderNav(buttonIds)
    buttonGroup = createObject("roSGNode", "ButtonGroup")

    for each buttonId in buttonIds
        if m.components[buttonId] <> invalid then
            button = createObject("roSGNode", "SpNativeButton")
            button.settings = m.components[buttonId].settings
            button.id = buttonId
            buttonGroup.appendChild(button)
        end if
    endfor

    m.colLeft.appendChild(buttonGroup)
    m.nav = buttonGroup
    m.nav.observeField("buttonSelected", "observeNav")
end sub

sub setCategoryData(event as Object)
    eventStatus = event.getData()

    if eventStatus = "stop" or eventStatus = "done" then
        m.privacyManagerViewData = m.privacyManagerViewTask.data

        if m.pmvCallback <> invalid then
            m.pmvCallback()
        end if
    end if
end sub

sub setConsent(event as Object)
    spMessage = m.top.getParent()
    spMessage.userConsent = event.getData()
    spMessage.done = true
end sub