import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.observeField("itemContent", "render")

    m.buttonPadding = scalePixelDimension(24)
    m.buttonTextSpacing = scalePixelDimension(15)

    m.button_background = m.top.findNode("button_background")
    m.button_label = m.top.findNode("button_label")
    m.button_label.translation = [m.buttonPadding, 0]

    m.rendered = false
end sub

sub render(event as Object)
    content = event.getData()
    settings = content.settings

    if m.rendered = true then
        renderTextColor(content.focused)
        return
    else
        m.rendered = true
    end if 

    ' defaults
    m.textColor = "0x000000FF"
    m.focusedTextColor = "0xFFFFFFFF"
    m.unFocusBackgroundColor = "0xFFFFFFFF"
    
    if settings.style <> invalid and settings.style <> invalid then
        if settings.style.onUnfocusTextColor <> invalid then
            m.textColor = colorConvert(settings.style.onUnfocusTextColor)
        end if
        if settings.style.onFocusTextColor <> invalid then
            m.focusedTextColor = colorConvert(settings.style.onFocusTextColor)
        end if
        if settings.style.onUnfocusBackgroundColor <> invalid then
            m.unFocusBackgroundColor = colorConvert(settings.style.onUnfocusBackgroundColor)
        end if
    end if

    m.top.id = content.id
    m.button_label.settings = settings

    addBackground(content)

    if content.carat <> "" and m.caratNode = invalid then
        addCarat(content)
    end if

    if content.on <> invalid and settings.onText <> invalid and settings.offText <> invalid and m.onOffNode = invalid then
        addOnOff(content, settings)
    end if

    ' if settings.customText <> invalid then
    '     addCustomText(content, settings)
    ' end if
    

    m.button_label.textComponent.vertAlign = "center"
    m.button_label.textComponent.height = content.height
    m.button_label.textComponent.width = content.width * .6

    renderTextColor(content.focused)
end sub

sub renderTextColor(focused as Boolean)
    if focused = true then
        textColor = m.focusedTextColor
    else
        textColor = m.textColor
    end if 
    m.button_label.textComponent.color = textColor

    if m.caratNode <> invalid then
        m.caratNode.color = textColor
    end if
    if m.customText <> invalid then
        m.customText.color = textColor
    end if
    if m.onOffNode <> invalid then
        m.onOffNode.color = textColor
    end if

    if focused = true then
        m.background.visible = false
    else 
        m.background.visible = true
    end if
end sub

sub addCarat(content as Object)
    m.caratNode = createObject("roSGNode", "SimpleLabel")
    m.caratNode.fontUri = "font:SystemFontFile"
    m.caratNode.fontSize = m.button_label.textComponent.font.size
    m.caratNode.text = content.carat
    m.caratNode.horizOrigin = "right"
    m.caratNode.vertOrigin = "center"
    m.caratNode.translation = [content.width  - m.buttonPadding, content.height / 2]
    m.top.appendChild(m.caratNode)
end sub

sub addBackground(content as Object)
    m.background = createObject("roSGNode", "Poster")
    m.background.height = content.height
    m.background.width = content.width
    m.background.uri = "pkg:/images/sourcepoint-sdk/focus_button.jpg"
    m.background.blendColor = m.unFocusBackgroundColor
    m.background.visible = false
    m.top.insertChild(m.background, 0)
end sub

sub addCustomText(content as Object, settings as Object)
    m.customText = createObject("roSGNode", "SimpleLabel")
    m.customText.text = settings.customText
    m.customText.fontUri = "font:SystemFontFile"
    m.customText.fontSize = m.button_label.textComponent.font.size
    
    rightOffset = 0
    if m.onOffNode <> invalid then
        rightOffset = m.onOffNode.boundingRect().x - m.buttonTextSpacing
    end if 
    m.customText.horizOrigin = "right"
    m.customText.vertOrigin = "center"
    m.customText.translation = [rightOffset, content.height / 2]
    m.top.appendChild(m.customText)
end sub

sub addOnOff(content as Object, settings as Object)
    m.onOffNode = createObject("roSGNode", "SimpleLabel")
    m.onOffNode.fontUri = "font:SystemFontFile"
    m.onOffNode.fontSize = m.button_label.textComponent.font.size
    if content.on = true then
        m.onOffNode.text = settings.onText
    else
        m.onOffNode.text = settings.offText
    endif
    rightOffset = content.width - m.buttonPadding
    if m.caratNode <> invalid then
        rightOffset = m.caratNode.boundingRect().x - m.buttonTextSpacing
    end if 
    m.onOffNode.horizOrigin = "right"
    m.onOffNode.vertOrigin = "center"
    m.onOffNode.translation = [rightOffset, content.height / 2]
    m.top.appendChild(m.onOffNode)
end sub