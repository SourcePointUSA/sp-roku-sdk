import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.itemSpacings = [scalePixelDimension(10)]
    m.top.observeField("buttonComponents", "render")
    m.top.observeField("buttonFocused", "updateFocus")
    m.top.observeField("focusedChild", "setFocus")
    m.buttonNodes = []
end sub

sub render(event as Object)
    buttons = event.getData()
    maxWidth = 0

    for each button in buttons
        buttonComponent = createObject("roSGNode", m.top.buttonComponentName)
        buttonComponent.settings = button.settings
        buttonComponent.id = button.id
        m.top.appendChild(buttonComponent)
        m.buttonNodes.push(buttonComponent)
        if buttonComponent.boundingRect().width > maxWidth then
            maxWidth = buttonComponent.boundingRect().width
        end if
    end for

    for each button in m.buttonNodes
        button.backgroundRect.width = maxWidth
    end for
end sub

sub setFocus(event as Object)
    focusedButton = event.getData()

    if focusedButton <> invalid then
        focusedButton.buttonFocused = true
    end if
end sub

' Update focus so buttons can handle their look and feel
sub updateFocus(event as Object)
    focused = event.getData()
    buttons = m.top.getChildren(-1, 0)

    for i = 0 to (buttons.count() - 1) step 1
        if i = focused then
            buttons[i].buttonFocused = true
        else
            buttons[i].buttonFocused = false
        end if
    end for
end sub