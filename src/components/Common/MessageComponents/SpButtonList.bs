import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.itemComponentName = "SpButtonListButton"
    m.top.observeField("buttonComponents", "render")
    m.top.observeField("itemFocused", "focusButton")
    m.top.observeField("itemUnfocused", "unFocusButton")

    ' default settings
    m.top.focusBitmapUri = ""
    m.top.focusFootprintBitmapUri = ""
    m.top.itemSpacing = [0, scalePixelDimension(10)]

    m.buttonNodes = createObject("roSGNode", "ContentNode")
end sub

sub render(event as Object)
    buttons = event.getData()

    buttonHeight = scalePixelDimension(64)
    width = 100
    if m.top.width <> invalid then
        width = m.top.width
    end if

    m.top.itemSize = [width, buttonHeight]
    m.top.numRows = 8

    buttonSettings = invalid
    first = true
    for each button in buttons
        buttonSettings = button.settings

        buttonContentNode = createObject("roSGNode", "SpButtonListContentNode")
        buttonContentNode.settings = button.settings
        buttonContentNode.id = button.id
        buttonContentNode.height = buttonHeight
        buttonContentNode.width = width

        ' fake that the first element is focused when rendering
        ' this will give us the correct focus footprint state
        if first = true then
            buttonContentNode.focused = true
            first = false
        end if
        if button.carat <> invalid then
            buttonContentNode.carat = button.carat
        end if
        if button.on <> invalid then
            buttonContentNode.on = button.on
        end if

        m.buttonNodes.appendChild(buttonContentNode)
    end for

    ' set up focus bitmap and focus footprint bitmap based on settings
    if buttonSettings <> invalid and buttonSettings.style.onFocusBackgroundColor <> invalid then
        m.top.focusBitmapUri = "pkg:/images/sourcepoint-sdk/focus_button.jpg"
        m.top.focusBitmapBlendColor = colorConvert(buttonSettings.style.onUnfocusBackgroundColor)
        m.top.focusFootprintBitmapUri = "pkg:/images/sourcepoint-sdk/focus_footprint_button.jpg"
        m.top.focusFootprintBlendColor = "0xA9A9A9FF"
    end if 

    m.top.content = m.buttonNodes
end sub

sub focusButton(event as Object)
    index = event.getData()

    setFocus(index, true)
end sub

sub setFocus(index as Integer, focused as Boolean)
    if index <> invalid then
        buttonCn = m.buttonNodes.getChild(index)
        if buttonCn <> invalid then
            buttonCn.focused = focused
        end if 
    end if
end sub

sub unFocusButton(event as Object)
    index = event.getData()

    setFocus(index, false)
end sub