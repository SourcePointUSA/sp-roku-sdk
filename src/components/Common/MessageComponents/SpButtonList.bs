import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.itemComponentName = "SpButtonListButton"
    m.top.observeField("buttonComponents", "render")
    m.top.observeField("itemFocused", "focusButton")
    m.top.observeField("itemUnfocused", "unFocusButton")

    ' default settings
    m.top.focusBitmapUri = ""
    m.top.focusFootprintBitmapUri = ""
    m.top.itemSpacing = [0, scalePixelDimension(10)]

    m.buttonNodes = createObject("roSGNode", "ContentNode")
    m.buttonNodesKeyed = {}

    m.rendered = false
end sub

sub render(event as Object)
    buttons = event.getData()

    buttonHeight = scalePixelDimension(64)
    width = 100
    if m.top.width <> invalid then
        width = m.top.width
    end if

    m.top.itemSize = [width, buttonHeight]
    m.top.numRows = 8

    buttonSettings = invalid
    first = true
    for each button in buttons
        buttonSettings = button.settings

        if button.id = invalid then
            button.id = button.settings.text
        end if
        buttonContentNode = m.buttonNodesKeyed[button.id]
        if buttonContentNode = invalid then
            buttonContentNode = createObject("roSGNode", "SpButtonListContentNode")
            buttonContentNode.id = button.id
            buttonContentNode.height = buttonHeight
            buttonContentNode.width = width

            ' fake that the first element is focused when rendering
            ' this will give us the correct focus footprint state
            if m.rendered = false and first = true then
                buttonContentNode.focused = true
                first = false
            end if
        end if
        buttonContentNode.settings = button.settings

        if button.carat <> invalid then
            buttonContentNode.carat = button.carat
        end if
        if button.on <> invalid then
            buttonContentNode.on = button.on
        end if

        if m.buttonNodesKeyed[button.id] = invalid then
            ' node is new, add it
            m.buttonNodesKeyed[button.id] = buttonContentNode
            m.buttonNodes.appendChild(buttonContentNode)
        end if
    end for

    if m.rendered = false then
        ' set up focus bitmap and focus footprint bitmap based on settings
        if buttonSettings <> invalid and buttonSettings.style.onFocusBackgroundColor <> invalid then
            m.top.focusBitmapUri = "pkg:/images/sourcepoint-sdk/focus_button.jpg"
            m.top.focusBitmapBlendColor = colorConvert(buttonSettings.style.onUnfocusBackgroundColor)
            m.top.focusFootprintBitmapUri = "pkg:/images/sourcepoint-sdk/focus_footprint_button.jpg"
            m.top.focusFootprintBlendColor = "0xA9A9A9FF"
        end if 

        m.top.content = m.buttonNodes
    end if

    m.rendered = true
end sub

sub focusButton(event as Object)
    index = event.getData()

    setFocus(index, true)
end sub

sub setFocus(index as Integer, focused as Boolean)
    if index <> invalid then
        buttonCn = m.buttonNodes.getChild(index)
        if buttonCn <> invalid then
            buttonCn.focused = focused
        end if 
    end if
end sub

sub unFocusButton(event as Object)
    index = event.getData()

    setFocus(index, false)
end sub