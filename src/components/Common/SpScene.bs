sub init()
    m.top.observeField("activeCampaign", "showMessage")
    m.top.observeField("campaigns", "incrementCampaign")
    m.userConsent = {}
end sub

sub closeMessage()
    if m.currentMessage <> invalid then
        m.top.removeChild(m.currentMessage)
        m.currentMessage.unobserveField("done")
        m.userConsent[m.currentMessage.messageCategory] = m.currentMessage.userConsent
        m.currentMessage = invalid
    end if

    incrementCampaign()
end sub

sub incrementCampaign()
    activeCampaign = m.top.activeCampaign
    if activeCampaign = invalid then
        activeCampaign = 0
    else 
        activeCampaign = activeCampaign + 1
    end if

    m.top.activeCampaign = activeCampaign
end sub

sub showMessage()
    m.currentMessage = CreateObject("roSGNode", "SpMessage")
    campaigns = m.top.campaigns
    campaign = campaigns[m.top.activeCampaign]

    if campaign <> invalid then
        m.currentMessage.observeField("done", "closeMessage")
        m.currentMessage.userConsent = campaign.userConsent
        m.currentMessage.message = campaign

        m.top.appendChild(m.currentMessage)
        m.currentMessage.setFocus(true)
    else 
        ' signal that we're done, return consent
        m.top.userConsent = m.userConsent
    end if
end sub