sub init()
    m.top.observeField("view", "renderView")
    m.top.observeField("privacyManagerViewData", "renderRightCol")
end sub

sub observeVendorList(event as Object)
    index = event.getData()

    if index <> invalid then
        item = m.vendorList.buttonComponents[index]
        changeView("VendorDetailsView", m.top.privacyManagerViewData.vendors[item.id])
    end if
end sub

sub renderRightCol()
    hideRightColLoader()

    if m.components.text_vendor_header <> invalid then
        vendorsHeader = createObject("roSGNode", "SpNativeText")
        vendorsHeader.settings = m.components.text_vendor_header.settings
        m.colRight.appendChild(vendorsHeader)
    end if

    buttons = []
    vendors = m.top.privacyManagerViewData.vendors
    for each vendorId in vendors
        buttonSettings = { 
            ' on: m.top.privacyManagerViewData.vendors[vendorId].enabled
            settings: {} 
        } 
        buttonSettings.settings.append(m.components.button_vendor.settings)
        buttonSettings.id = vendorId
        buttonSettings.settings.text = vendors[vendorId].name

        buttons.push(buttonSettings)
    end for

    if buttons.count() > 0 then
        if m.vendorList = invalid then
            m.vendorList = createObject("roSGNode", "SpButtonList")
            m.vendorList.id = "vendor_list"
            m.vendorList.width = m.colRightWidth
            
            m.colRight.appendChild(m.vendorList)
            m.rightColFocus = m.vendorList
            m.vendorList.observeField("itemSelected", "observeVendorList")
        end if 

        m.vendorList.buttonComponents = buttons
    end if
end sub

sub renderView(event as Object)
    hasPmvData = getPrivacyManagerViewData(2)

    renderRightColLoader()

    view = event.getData()
    mapComponents(view)

    renderLogo()
    renderNav([])

    setFocus(m.back_button)
end sub