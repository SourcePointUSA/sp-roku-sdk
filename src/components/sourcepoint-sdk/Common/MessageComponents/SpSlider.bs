sub init()
    m.top.focusable = true
    m.top.observeField("focusedChild", "setFocus")
    m.top.observeField("settings", "render")

    m.buttonGroup = m.top.findNode("button-group")
    m.top.buttonLeft = m.top.findNode("button-left")
    m.top.buttonRight = m.top.findNode("button-right")

    m.focusedChild = invalid
    m.top.buttonSelected = "left"
end sub

function onKeyEvent(key as String, press as Boolean) as Boolean
    if key = "right" then 
        m.top.buttonRight.setFocus(true)
        m.top.buttonSelected = "right"
        return true
    else if key = "left" then
        m.top.buttonLeft.setFocus(true)
        m.top.buttonSelected = "left"
        return true
    else if key = "OK" then
        return true
    end if 

    return false
end function

sub setFocus(event as Object)
    focusedChild = event.getData()

    if m.top.isSameNode(focusedChild) then
        if m.focusedChild <> invalid then
            m.focusedChild.setFocus(true)
        else
            m.buttonGroup.getChild(0).setFocus(true)
        end if
    else
        m.focusedChild = focusedChild
    end if
end sub

sub render(event as Object)
    settings = event.getData()

    m.top.buttonLeft.settings = settings
    m.top.buttonRight.settings = settings

    m.top.buttonLeft.showFocusFootprint = false
    m.top.buttonRight.showFocusFootprint = false

    if settings.leftText <> invalid then
        m.top.buttonLeft.text = settings.leftText
    end if
    if settings.rightText <> invalid then
        m.top.buttonRight.text = settings.rightText
    end if
end sub