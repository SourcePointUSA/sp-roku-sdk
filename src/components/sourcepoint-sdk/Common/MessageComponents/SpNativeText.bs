import "pkg:/source/sourcepoint-sdk/Helpers.bs"

sub init()
    m.top.observeField("settings", "render")
    m.top.observeField("text", "renderText")
end sub

sub render(event as Object)
    componentName = m.top.componentName
    settings = event.getData()

    textComponent = createObject("roSGNode", componentName)

    m.top.textComponent = textComponent
    
    if settings.text <> invalid then
        m.top.text = settings.text
    end if 

    if settings.style <> invalid and settings.style.font <> invalid then
        if settings.style.font.color <> invalid then
            textComponent.color = colorConvert(settings.style.font.color)
        else
            textComponent.color = "0x000000FF"
        end if
        if componentName = "Label" or componentName = "ScrollingLabel" or componentName = "ScrollableText" then
            font = createFont(settings.style.font)
            textComponent.font = font
        else if componentName = "SimpleLabel" then
            if settings.style.font.fontWeight <> invalid and settings.style.font.fontWeight = "700" then
                textComponent.fontUri = "font:BoldSystemFontFile"
            else
                textComponent.fontUri = "font:SystemFontFile"
            end if
            if settings.style.font.fontSize <> invalid then
                textComponent.fontSize = settings.style.font.fontSize
            end if
        end if
    end if

    m.top.appendChild(textComponent)
end sub

sub renderText(event as Object)
    text = event.getData()

    if text <> invalid then
        m.top.textComponent.text = stripHtmlTags(text)
    else 
        m.top.textComponent.text = ""
    end if
end sub