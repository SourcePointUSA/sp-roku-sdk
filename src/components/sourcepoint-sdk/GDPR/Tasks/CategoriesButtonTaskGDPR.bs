sub init()
    m.top.functionName = "createButtonSections"
end sub

sub createButtonSections()
    buttonsLi = []
    sections = []

    try
        buttonCategorySettings = m.top.buttonCategorySettings
        categoryTypes = m.top.categoryTypes
        categoryTypeOrder = ["categories", "specialPurposes", "specialFeatures"]

        if m.top.privacyManagerViewData.legIntCategories <> invalid then
            for each id in m.top.privacyManagerViewData.legIntCategories
                buttonSettings = { 
                    categoryType: "legInt",
                    on: m.top.privacyManagerViewData.legIntCategories[id].enabled,
                    settings: {},
                    showCustom: m.top.privacyManagerViewData.legIntCategories[id].type = "CUSTOM"
                } 

                buttonSettings.settings.append(buttonCategorySettings)
                buttonSettings.id = id
                buttonSettings.settings.text = m.top.privacyManagerViewData.legIntCategories[id].name
        
                buttonsLi.push(buttonSettings)
            end for
        end if
        
        categoryTypeOrder = ["categories", "specialPurposes", "specialFeatures"]
        sections = []
        for each cType in categoryTypeOrder
            buttons = []

            if m.top.privacyManagerViewData[cType] <> invalid then
                for each id in m.top.privacyManagerViewData[cType]
                    listItem = m.top.privacyManagerViewData[cType][id]
                    if (listItem.vendors <> invalid and listItem.vendors.count() > 0) or (listItem.requiringConsentVendors <> invalid and listItem.requiringConsentVendors.count() > 0) then
                        buttonSettings = {
                            categoryType: cType, 
                            on: m.top.privacyManagerViewData[cType][id].enabled,
                            settings: {},
                            showCustom: m.top.privacyManagerViewData[cType][id].type = "CUSTOM"
                        }

                        buttonSettings.settings.append(buttonCategorySettings)
                        buttonSettings.id = id
                        buttonSettings.settings.text = m.top.privacyManagerViewData[cType][id].name
                
                        buttons.push(buttonSettings)
                    end if
                end for
            end if
        
            if buttons.count() > 0 then
                section = {
                    children: buttons
                }

                if categoryTypes[cType].headerComponent <> invalid then
                    section.settings = categoryTypes[cType].headerComponent.settings
                end if

                if categoryTypes[cType].defComponent <> invalid then
                    section.settingsDesc = categoryTypes[cType].defComponent.settings
                end if

                sections.push(section)
            end if
        end for
    catch e
        m.top.error = e.message
    end try

    m.top.buttons = {
        "buttonsLi": buttonsLi,
        "sections": sections
    }
end sub