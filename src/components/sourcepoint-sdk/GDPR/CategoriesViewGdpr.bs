sub init()
    m.top.observeField("view", "renderView")
    m.top.observeField("privacyManagerViewData", "renderRightCol")

    m.categoryLists = {}
end sub

sub changeCategories(event as Object)
    buttonSide = event.getData()

    if buttonSide = "left" then
        m.categoryLists["right"].visible = false
        m.categoryLists["left"].visible = true

        m.rightColFocus = m.categoryLists["left"]
    else
        m.categoryLists["left"].visible = false
        m.categoryLists["right"].visible = true

        m.rightColFocus = m.categoryLists["right"]
    end if
end sub

sub observeCategoryList(event as Object)
    item = m.categoryList.focusedContentNode

    cType = "categories"
    ' TODO - we are not getting the correct categories here
    if item.categoryType <> invalid then
        cType = item.categoryType
    end if

    viewData = {"categoryType": cType}
    viewData.append(m.top.privacyManagerViewData.[cType][item.id])

    changeView("CategoryDetailsView", viewData)
end sub

function onKeyEvent(key as String, press as Boolean) as Boolean
    if key = "up" and m.nav.isInFocusChain() = true then 
        m.categorySlider.setFocus(true)
        return true
    else if key = "down" and m.categorySlider.isInFocusChain() = true then
        m.nav.setFocus(true)
        return true
    end if 

    return _onKeyEvent(key, press)
end function

sub renderCategoryDescription()
    if m.categoryDescription = invalid then
        m.categoryDescription = createObject("roSGNode", "SpNativeText")
        m.categoryDescription.settings = m.components.text_category_description.settings
        m.categoryDescription.textComponent.wrap = true
    end if
end sub

sub renderRightCol()
    categoryTypes = {
        "categories": {
            headerComponent: m.components.text_purposes_header,
            defComponent: m.components.text_purposes_def
        }, 
        "specialPurposes": {
            headerComponent: m.components.text_special_purposes_header,
            defComponent: m.components.text_special_purposes_def
        }, 
        "specialFeatures": {
            headerComponent: m.components.text_features_header,
            defComponent: m.components.text_features_def
        }
    }

    categoryTypeOrder = ["categories", "specialPurposes", "specialFeatures"]

    sections = []
    buttonsLi = []
    for each cType in categoryTypeOrder
        buttons = []

        for each id in m.top.privacyManagerViewData[cType]
            listItem = m.top.privacyManagerViewData[cType][id]

            if (listItem.vendors <> invalid and listItem.vendors.count() > 0) or (listItem.requiringConsentVendors <> invalid and listItem.requiringConsentVendors.count() > 0) then
                buttonSettings = {
                    categoryType: cType, 
                    on: m.top.privacyManagerViewData[cType][id]["__enabled"]
                    settings: {} 
                } 
                buttonSettings.settings.append(m.components.button_category.settings)
                buttonSettings.id = id
                buttonSettings.settings.text = m.top.privacyManagerViewData[cType][id].name
        
                buttons.push(buttonSettings)
            end if
            if listItem.legIntVendors <> invalid and listItem.legIntVendors.count() > 0 then
                buttonSettings = { 
                    categoryType: "legInt",
                    on: m.top.privacyManagerViewData[cType][id]["__enabled_li"]
                    settings: {} 
                } 
                buttonSettings.settings.append(m.components.button_category.settings)
                buttonSettings.id = id
                buttonSettings.settings.text = m.top.privacyManagerViewData[cType][id].name
        
                buttonsLi.push(buttonSettings)
            end if
        end for
    
        if buttons.count() > 0 then
            section = {
                children: buttons,
                settings: categoryTypes[cType].headerComponent.settings
            }

            sections.push(section)
        end if
    end for

    m.categoryListTarget = createObject("roSGNode", "Group")
    m.colRight.appendChild(m.categoryListTarget)
    
    ' Create category list
    if m.categoryList = invalid then
        m.categoryList = createObject("roSGNode", "SpHeaderButtonList")
        m.categoryList.width = m.colRightWidth

        m.categoryListTarget.appendChild(m.categoryList)
        m.rightColFocus = m.categoryList
        m.categoryList.observeField("itemSelected", "observeCategoryList")
        if m.components.text_purposes_header <> invalid then
            m.categoryList.sectionSettings = m.components.text_purposes_header.settings
        end if

        m.categoryLists["left"] = m.categoryList
    end if 
    ' Create LI category list if needed
    if buttonsLi.count() > 0 and m.categoryListLi = invalid then
        m.categoryListLi = createObject("roSGNode", "SpHeaderButtonList")
        m.categoryListLi.width = m.colRightWidth
        m.categoryListLi.visible = false

        m.categoryListTarget.appendChild(m.categoryListLi)
        m.categoryListLi.observeField("itemSelected", "observeCategoryList")

        m.categoryListLi.buttonComponents = buttonsLi
        m.categoryLists["right"] = m.categoryListLi
    end if

    m.categoryList.buttonComponents = sections
end sub

sub renderSlider()
    if m.categorySlider = invalid then
        categorySlider = createObject("roSGNode", "SpSlider")
        categorySlider.settings = m.components.slider_categories.settings
        titleHolder = m.top.findNode("title-holder")
        titleHolder.appendChild(categorySlider)
        m.categorySlider = categorySlider
        m.categorySlider.observeField("buttonSelected", "changeCategories")
    end if
end sub

sub renderView(event as Object)
    getPrivacyManagerViewData(1)
    m.colLeft = m.top.findNode("col-left")
    m.colRight = m.top.findNode("col-right")

    view = event.getData()
    mapComponents(view)

    renderLogo()
    renderCategoryDescription()
    renderNav(["save_and_exit"])
    renderSlider()

    ' TODO add loader to the right col until it is rendered

    m.nav.setFocus(true)
end sub