function addQueryParams(uri as String, params as Object) as String
    separator = uri.Instr(0, "?") < 0 ? "?" : "&"

    for each k in params
        if params[k] <> invalid
            uri = uri + separator + k + "=" + params[k].ToStr().Escape()
            separator = "&"
        end if
    end for

    return uri
end function

function getLocalState() as String
    registry = CreateObject("roRegistrySection", "SourcepointSdk")
    if registry.Exists("localState") then
        return registry.read("localState")
    end if

    return ""
end function

sub setLocalState(localState)
    registry = CreateObject("roRegistrySection", "SourcepointSdk")

    registry.write("localState", localState)
    registry.flush()
end sub

sub updateTranslations(node as object, translationMap as Object)
    resolutionInfo = m.top.getScene().currentDesignResolution

    for each nodeId in translationMap
        updateNode = node.findNode(nodeId)
        if updateNode <> invalid
            if type(translationMap[nodeId]) = "roFunction"
                translate = translationMap[nodeId](updateNode, resolutionInfo.width, resolutionInfo.height)
            else
                translate = translationMap[nodeId]
            end if

            if resolutionInfo.resolution = "FHD"
                translate[0] = translate[0] * 1.5
                translate[1] = translate[1] * 1.5
            end if
            
            updateNode.translation = translate
        end if
    end for
end sub